/**********************************************************************************
*
* MAD-X input script for the moment of resonance crossing on the SFTPRO cycle, 
* corresponding to 720 ms cycle time. 
* 14/08/2019 - Alexander Huschauer
************************************************************************************/
 
/******************************************************************
 * Energy and particle type definition
 ******************************************************************/

BEAM, PARTICLE=PROTON, PC = 14.;
BRHO      := BEAM->PC * 3.3356;

/******************************************************************
 * Call lattice files
 ******************************************************************/

call, file="../../../ps_mu.seq";
call, file="../../../ps_ss.seq";
call, file="../../../ps.str";
call, file="./ps_rc_sftpro.str";
call, file="../../../macros.ptc";

/******************************************************************
 *                   Tune matching with LEQ
 ******************************************************************/

Qx := 0.2500;
Qy := 0.29826;

use, sequence=PS;
match,use_macro;
        vary, name = kf, step = 1.0E-6;
        vary, name = kd, step = 1.0E-6;
        use_macro, name = ptc_twiss_tune_macro;
        constraint, expr = table(ptc_twiss_summary,Q1) = Qx;
        constraint, expr = table(ptc_twiss_summary,Q2) = Qy;
jacobian,calls=50000,bisec=3,tolerance=1.0e-21;
ENDMATCH;

assign, echo="./ps_rc_sftpro_tune_matching.dat";
value, kf, kd;
Assign, echo=terminal;

/******************************************************************
 * PTC Twiss
 ******************************************************************/

use, sequence=PS;
select, flag=ptc_twiss, column=name,keyword,s,x,px,y,py,t,pt,beta11,alfa11,beta22,alfa22,disp1,disp,disp3,disp4,gamma11,gamma22,mu1,mu2,energy,l,angle,K0L,K0SL,K1L,K1SL,K2L,K2SL,K3L,K3SL,K4L,K4SL,K5L,K5SL,VKICK,HKICK,SLOT_ID;
ptc_create_universe;
ptc_create_layout, model=2, method=6, nst=5, exact=true,;
ptc_twiss,closed_orbit,icase=56,no=4, file = './ps_rc_sftpro.tfs';
ptc_end;

!stop;

! obtain smooth optics functions by slicing the elements
use, sequence=PS;
select, flag=ptc_twiss, column=name,keyword,s,x,px,y,py,t,pt,beta11,alfa11,beta22,alfa22,disp1,disp,disp3,disp4,gamma11,gamma22,mu1,mu2,energy,l,angle,K0L,K0SL,K1L,K1SL,K2L,K2SL,K3L,K3SL,K4L,K4SL,K5L,K5SL,VKICK,HKICK,SLOT_ID;
ptc_create_universe;
ptc_create_layout, model=2, method=6, nst=5, exact=true,;
ptc_twiss,closed_orbit,icase=56,no=4,slice_magnets=true, file = './ps_rc_sftpro_interpolated.tfs';
ptc_end;

